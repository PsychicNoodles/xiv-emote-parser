// supported tags
tag_clickable = { "Clickable" }
tag_sheet = { "Sheet" }
tag_sheet_en = { "SheetEn" }

// supported functions
func_equal = { "Equal" }
func_obj_param = { "ObjectParameter" }
func_player_param = { "PlayerParameter" }

// low level types
text = { (LETTER | PUNCTUATION | SEPARATOR)+ }
// tag_name = { ASCII_ALPHA_UPPER ~ ASCII_ALPHA+ }
tag_name = { tag_sheet_en | tag_sheet | tag_clickable }
// func_name = { ASCII_ALPHA_UPPER ~ ASCII_ALPHA+ }
func_name = { func_equal | func_obj_param | func_player_param }
param_num = { ASCII_DIGIT+ }
param_obj = { ASCII_ALPHA_UPPER ~ ASCII_ALPHA+ }

// tags
open_tag = { "<" ~ tag_name ~ ("(" ~ param ~ ("," ~ param)* ~ ")")? ~ ">" }
auto_closing_tag = { "<" ~ tag_name ~ ("(" ~ param ~ ("," ~ param)* ~ ")")? ~ "/>" }
close_tag = { "</" ~ tag_name ~ ">" }

// elements
if_else_then = { param | text }
if_else_element = { "<If(" ~ param ~ ")>" ~ if_else_then ~ "<Else/>" ~ if_else_then ~ "</If>" }
element = {
  if_else_element |
  open_tag ~ text? ~ close_tag |
  auto_closing_tag
}

// high level types
function = { func_name ~ "(" ~ param ~ ("," ~ param)* ~ ")" }
param = { element | param_num | function | param_obj }
message_part = { element | text }
message = { SOI ~ message_part+ ~ EOI }
